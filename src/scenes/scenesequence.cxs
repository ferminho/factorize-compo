Strict

Public

Import cerberus.random
Import mojo2

Import scenes.scene

Class SceneSequence Implements Scene

	Method New(scenes:Scene[], interruptible:Bool = True)
		Self.scenes = scenes
		Self.interruptible = interruptible
	End Method

	Method StartIn:Void(sceneNumber:Int)
		current = sceneNumber
		Start()
	End Method

	Method Start:Void()
		scenes[current].Start()
	End Method
	
	Method Resume:Void()
	End Method

	Method Update:Int()
		Local status:Int = scenes[current].Update()
		If (status = Scene.Complete)
			current += 1
			If (current > scenes.Length)
				Return Scene.Complete
			End If
			scenes[current].Start()
		Else If (status = Scene.Interrupted)
			If (interruptible Or current = 0)
				Return Scene.Interrupted
			End If
			current -= 1
			scenes[current].Resume()
		End If
		Return Scene.Running
	End Method

	Method Draw:Void(canvas:Canvas)	
		scenes[current].Draw(canvas)
	End Method

	Private
	
	Field scenes:Scene[]
	Field current:Int = 0
	Field interruptible:Bool
	
End Class