Strict

Import mojo2 

Import scenes.editing
Import scenes.scene
Import world.levelmap
Import world.levelmapfactory
Import world.tileset

Class Level Implements Scene

	Method Start:Void()
		Local levelMap:LevelMap = LevelMapFactory.LoadFromFile("levels/test.json")
		editingScene = New Editing(levelMap)
	End Method

	Method Update:Int()
		Local status:Int
		If (playingScene <> Null)
			Return UpdatePlayingScene()
		Else
			Return UpdateEditingScene()
		End If
		Return Scene.KeepRunning
	End Method
	
	Method Draw:Void(canvas:Canvas)	
		If (playingScene <> Null)
			playingScene.Draw(canvas)
		Else
			editingScene.Draw(canvas)
		End If
	End Method

	Private
	
	Field editingScene:Editing
	Field playingScene:Playing

	Method UpdatePlayingScene:Int()
		Local status:Int = playingScene.Update()
		
		If (status = Scene.Abort)
			playingScene = Null
			Return UpdateEditingScene()
		Else If (status = Scene.SkipToNextScene)
			Return Scene.SkipToNextScene
		End If
		Return Scene.KeepRunning
	End Method
	
	Method UpdateEditingScene:Int()
		Local status:Int = editingScene.Update()

		If (status = Scene.SkipToNextScene)
			playingScene = editingScene.StartPlaying()
			Return playingScene.Update()
		End If

		Return status
	End Method
	
End Class