Strict

Import mojo2 

Import scenes.editing
Import scenes.scene
Import world.camera
Import world.levelmap
Import world.levelmapfactory
Import world.tileset

Class Level Implements Scene

	Method Start:Void()
		levelMap = LevelMapFactory.LoadFromFile("levels/test.json")
		camera = New Camera(levelMap)

		editingScene = New Editing(levelMap, camera)
		editingScene.Start()
	End Method

	Method Resume:Void()
	End Method

	Method Update:Int()
		Local status:Int
		If (playingScene <> Null)
			Return UpdatePlayingScene()
		Else
			Return UpdateEditingScene()
		End If
		Return Scene.Running
	End Method
	
	Method Draw:Void(canvas:Canvas)	
		If (playingScene <> Null)
			playingScene.Draw(canvas)
		Else
			editingScene.Draw(canvas)
		End If
	End Method

	Private
	
	Field levelMap:LevelMap
	Field camera:Camera
	
	Field editingScene:Editing
	Field playingScene:Playing

	Method UpdatePlayingScene:Int()
		Local status:Int = playingScene.Update()
		
		If (status = Scene.Interrupted)
			playingScene = Null
			editingScene.Resume()
		Else If (status = Scene.Complete)
			Return Scene.Complete
		End If
		Return Scene.Running
	End Method
	
	Method UpdateEditingScene:Int()
		Local status:Int = editingScene.Update()

		If (status = Scene.Complete)
			playingScene = New Playing(levelMap, camera)
			playingScene.Start()
			Return Scene.Running
		End If

		Return status
	End Method
	
End Class