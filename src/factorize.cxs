Strict 

Import cerberus.random
Import mojo2

Import consts
Import scenes.game
Import scenes.loading
Import time
Import ui.screen

Class Factorize Extends App
Private
	Const DesiredUps:Float = 30.0
	Const UpdateTime:Int = 1000.0 / DesiredUps

	Field canvas:Canvas
	Field scenes:Scene[] = [Scene(New Loading()), Scene(New Game())]
	Field currentScene:Int = 0
	
	Field nextExpectedUpdate:Int 

	Method OnCreate:Int()
		canvas = New Canvas()
		canvas.SetProjection2d(0.0, CANVASWIDTH, 0.0, CANVASHEIGHT)
		Screen.Instance.Update()
		
		scenes[0].Start()
		
		nextExpectedUpdate = Millisecs()
		Return 0
	End Method
	
	Method OnUpdate:Int()
		Local time:Int = Millisecs()

		If (time >= nextExpectedUpdate)
			Screen.Instance.Update()
			Time.Instance.Update()
			Mouse.Instance.Update()
			UpdateScenes()
			nextExpectedUpdate = time + UpdateTime
		End If
		
		Return 0
	End Method
	
	Method OnRender:Int()
		canvas.Clear()
		scenes[currentScene].Draw(canvas)
		canvas.Flush()
		
		Return 0
	End Method

	Method UpdateScenes:Void()
		Local status:Int = scenes[currentScene].Update()
		
		If (status = Scene.SkipToNextScene)
			currentScene+= 1
			scenes[currentScene].Start()
		Else If (status = Scene.Abort)
			currentScene-= 1
			If (currentScene < 0) Then EndApp()
			scenes[currentScene].Start()
		End If
	End Method
End Class

Function Main:Int()
	New Factorize()
	Return 0
End