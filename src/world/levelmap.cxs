Strict

Import mojo2

Import consts
Import world.camera
Import world.tileset
Import time

Class LevelMap

	Const MASKTILE:Int = $FF
	Const MASKDIRECTION:Int = $700

	Const DIRECTIONRIGHT:Int = $000
	Const DIRECTIONDOWN:Int = $100
	Const DIRECTIONLEFT:Int = $200
	Const DIRECTIONUP:Int = $300
	Const MIRRORX:Int = $400
	Const MIRRORY:Int = $500
	Const MIRRORXY:Int = $600

	Field width:Int
	Field height:Int

	Field groundTiles:Int[]
	Field playerBlocks:Int[]
	Field fixedBlocks:Int[]

	Method New(width:Int, height:Int)
		Self.width = width
		Self.height = height
		groundTiles = New Int[width * height]
		fixedBlocks = New Int[width * height]
		playerBlocks = New Int[width * height]
	End Method

	Method Update:Void()
		animDelta = Floor(Time.Instance.actTime * TILEANIMSPEED / 1000.0)
	End Method

	Method DrawBottomLayers:Void(canvas:Canvas, camera:Camera)
		DrawTilemaps(canvas, [groundTiles, playerBlocks], camera)
	End Method
	
	Method DrawTopLayers:Void(canvas:Canvas, camera:Camera)
		DrawTilemaps(canvas, [fixedBlocks], camera)
	End Method

	Method GetGroundTileAt:Int(x:Float, y:Float)
		Local i:Int = Floor((y - 0.5) / TILESIZE)
		Local j:Int = Floor((x + 0.5) / TILESIZE)
		If (i < 0 Or i >= height) Then Return Tileset.ABYSS
		If (j < 0 Or j >= width) Then Return Tileset.ABYSS
		Return groundTiles[i * width + j]
	End Method

Private

	Field animDelta:Int = 0

	Method DrawTilemaps:Void(canvas:Canvas, tilemaps:Int[][], camera:Camera)
		Local x0:Int = -(camera.x0 Mod TILESIZE) + HALFTILESIZE
		Local y:Int = -(camera.y0 Mod TILESIZE) + HALFTILESIZE

		Local tileI0:Int = camera.y0 / TILESIZE
		Local tileI1:Int = tileI0 + (CANVASHEIGHT / TILESIZE)
		Local tileJ0:Int = camera.x0 / TILESIZE
		Local tileJ1:Int = tileJ0 + (CANVASWIDTH / TILESIZE)
		
		For Local i:Int = tileI0 To tileI1
			If (i >= 0 And i < height)
				Local x:Int = x0
				For Local j:Int = tileJ0 To tileJ1
					If (j >= 0 And j < width)
						DrawTilesAt(canvas, tilemaps, i * width + j, x, y)
					End If
					x += TILESIZE
				End For
			End If
			y += TILESIZE
		End For
	End Method
	
	Method DrawTilesAt:Void(canvas:Canvas, tilemaps:Int[][], position:Int, x:Int, y:Int)
		Local imgN:Int
		Local animFrames:Int
		Local img:Image
		Local direction:Int
		Local angle:Float = 0.0
		Local scaleX:Float = 1.0
		Local scaleY:Float = 1.0
		
		For Local tilemap:Int[] = EachIn tilemaps
			imgN = tilemap[position]
			direction = imgN & MASKDIRECTION
			imgN &= MASKTILE
			If (imgN > 0)
				animFrames = TileSet.AnimFrames[imgN]
				If (animFrames > 1) Then imgN += animDelta Mod animFrames
				img = TileSet.GfxTiles[imgN]
				
				If (direction = DIRECTIONDOWN)
					angle = -90.0
				ElseIf (direction = DIRECTIONLEFT)
					scaleX = -1.0
				ElseIf (direction = DIRECTIONUP)
					scaleX = -1.0
					angle = -90.0
				ElseIf (direction = MIRRORX)
					scaleX = -1.0
				ElseIf (direction = MIRRORY)
					scaleY = -1.0
				ElseIf (direction = MIRRORXY)				
					scaleX = -1.0
					scaleY = -1.0
				EndIf

				canvas.DrawImage(img, x, y, angle, scaleX, scaleY)
			End If
		End For
	End Method
	
End Class