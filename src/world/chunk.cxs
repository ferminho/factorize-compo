Strict

Import mojo2

Import consts
Import logger
Import world.camera
Import world.tileset

Class Chunk
	Field x:Float, y:Float	
	Field blocks:Int[][] = New Int[0][]
	
	Method New()
	End Method
	
	Method New(width:Int, height:Int)
		blocks = New Int[height][]
		For Local i:Int = 0 To height - 1
			blocks[i] = New Int[width]
		End For
	End Method
	
	Method Width:Int() Property
		Return blocks[0].Length
	End Method
	
	Method Height:Int() Property
		Return blocks.Length
	End Method

	Method Draw:Void(canvas:Canvas, camera:Camera)
		Local img:Int = 0
		Local drawX:Float
		Local drawY:Float = y + HALFTILESIZE - camera.y0

		For Local i:Int = 0 To Height - 1
			drawX = x + HALFTILESIZE - camera.x0
			For Local j:Int = 0 To Width - 1
				img = blocks[i][j]
				If (img > 0)
					canvas.DrawImage(TileSet.GfxTiles[img], drawX, drawY)
				End If
				drawX += TILESIZE
			End For
			drawY += TILESIZE
		End For
	End Method
	
	Method AddBlock:Void(block:Int, i:Int, j:Int) ' i ~ y (row), j ~ x (col)
		If (block = 0) 
			Logger.Error("Tried to add a zero block to a chunk")
		Else If (i < Height And j < Width And blocks[i][j] <> 0)
			Logger.Error("Tried to add a block at an existing block place")
		Else
			If (j >= Width)
				For Local i:Int = 0 To Height - 1
					blocks[i] = blocks[i].Resize(j + 1)
				End For
			End If
			If (i >= Height)
				Local oldHeight:Int = Height
				blocks = blocks.Resize(i + 1)
				For Local i:Int = oldHeight To Height - 1
					blocks[i] = New Int[Width]
				End For
			End If
			
			blocks[i][j] = block
		End If
	End Method
	
	Method RemoveBlock:Void(i:Int, j:Int) ' i ~ y (row), j ~ x (col)
		If (i >= Height Or j >= Width)
			Logger.Error("Tried to remove a block out of bounds")
		Else If (blocks[i][j] = 0)
			Logger.Error("Tried to remove an empty block")
		Else
			blocks[i][j] = 0
			TrimBlocks()
		End If
	End Method
	
	Private
	
	Method TrimBlocks:Void()
		Local lastFilledCol:Int = -1
		Local lastFilledRow:Int = -1
		
		For Local i:Int = 0 To Height - 1
			For Local j:Int = 0 To Width - 1
				If (blocks[i][j] <> 0)
					If (i > lastFilledRow) Then lastFilledRow = i
					If (j > lastFilledCol) Then lastFilledCol = j
				End If
			End For
		End For
		
		If (lastFilledRow + 1 < Height)
			blocks = blocks.Resize(lastFilledRow + 1)
		End If
		If (lastFilledCol + 1 < Width)
			For Local i:Int = 0 To Height - 1
				blocks[i] = blocks[i].Resize(lastFilledCol + 1)
			End For
		End If
	End Method

End Class