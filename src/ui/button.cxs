Strict

Import mojo2

Import time
Import ui.mouse

Class Button

	Const ICONPLAY:Int = 0
	Const ICONPAUSE:Int = ICONPLAY + 1
	Const ICONSTOP:Int = ICONPAUSE + 1
	Const ICONRETRY:Int = ICONSTOP + 1

	Function Initialize:Void()
		GfxIcons = Image.LoadFrames("cerberus://data/gfx/icons.png", NICONS, False, .5, .5, 0)
	End Function

	Method New(icon:Int, x:Float, y:Float)
		Self.icon = icon
		Self.x = x
		Self.y = y
		halfWidth = GfxIcons[icon].Width() / 2.0
		halfHeight = GfxIcons[icon].Height() / 2.0
	End Method

	Method Update:Void()
		Local delta:Int = (Time.Instance.realActTime Mod 3000) + 1
		If (delta > 2750)
			highlightLevel = Float(delta - 2750) / 500.0
		Else
			highlightLevel = 0.0
		End If
	End Method

	Method Draw:Void(canvas:Canvas)
		' Shadow
		canvas.SetColor(0.0, 0.0, 0.0)
		canvas.SetAlpha(0.7)
		canvas.DrawImage(GfxIcons[icon], x, y + 1.0)
		' Icon
		canvas.SetColor(1.0, 1.0, 1.0)
		canvas.SetAlpha(1.0)
		canvas.DrawImage(GfxIcons[icon], x, y)
		' Highlight
		canvas.SetBlendMode(BlendMode.Additive)
		canvas.SetAlpha(highlightLevel)
		canvas.DrawImage(GfxIcons[icon], x, y)
		
		canvas.SetBlendMode(BlendMode.Alpha)
		canvas.SetAlpha(1.0)
	End Method

	Method IsClicked:Bool() Property
		Local mouse:Mouse = Mouse.Instance
		If (Not mouse.HasBeenHandled And mouse.buttonState = Mouse.CLICK)
			Return mouse.x >= x - halfWidth And mouse.x <= x + halfWidth And mouse.y >= y - halfHeight And mouse.y <= y + halfHeight
		End If
		Return False
	End Method

	Private

	Const NICONS:Int = 4

	Global GfxIcons:Image[]

	Field icon:Int
	Field x:Float, y:Float
	Field halfWidth:Float, halfHeight:Float
	Field highlightLevel:Float = 0.0
	
End Class
